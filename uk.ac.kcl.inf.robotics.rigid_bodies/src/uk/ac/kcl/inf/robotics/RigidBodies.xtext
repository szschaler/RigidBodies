// TODO Fix scope providers so that the various references actually work as if the names were global
grammar uk.ac.kcl.inf.robotics.RigidBodies with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate rigidBodies "http://www.ac.uk/kcl/inf/robotics/RigidBodies"

Model:
	defs += InitialDefinition*
	world = Environment
	bodies += System+
;
	
InitialDefinition:
	BaseMatrix |
	JointType 
;	
	
Environment:
	'world' '{'
		'gravity' gravity=Matrix // TODO Check it's actually a three-element vector 
	'}'
;

System:
	'system' name=ID '{'
		elements += SystemElement+
	'}'
;

SystemElement:
	Body |
	Joint |
	Constraint |
	ExternalLoad
;

Body:
	'body' name=ID '{'
		// frame = LocalFrame // We're assuming the local frame is attached to the first joint for this body.
		mass  = Mass 
	'}'
;

Mass:
	'mass' '{' 
		'value' value = AddExp
		'position' position = Matrix // TODO Check it's actually a three-element vector
		'inertia' inertia = Matrix // TODO Check it's actually a three-by-four matrix
	'}'
;

Joint:
	'joint' name=ID ':' type=JointType 
		'between' body1 = [Body] /* TODO Add support for 'base' */ relTrans1 = RelativeTransformation
		'and' body2 = [Body] relTrans2 = RelativeTransformation
		// TODO We probably want to make types more complex to allow them to make stiffness reusable
		'stiffness' '{'
			// TODO Provide better labels for these in the concrete syntax
			'springCoeff' springCoeff = AddExp
			'springInit' springInit = AddExp
			'dampViscous' dampViscous = AddExp
			'dampCoulomb' dampCoulomb = AddExp
		 '}'
;

JointType:
	('joint' 'type' name=ID)? exp=JointTypeExpression
;

JointTypeExpression:
	ref=[JointType] |
	'revolute' '(' axis+=AXIS (',' axis+=AXIS)* ')' |
	'planar' '(' axis+=AXIS (',' axis+=AXIS)* ')'
;

enum AXIS: 
	X = 'x' | Y = 'y' | Z = 'z'
;

RelativeTransformation:
	'(' 'with' 'relative' 'transformation' position=Matrix /* TODO This is a 3D vector */ ',' reorient=Reorientation ')'
;

Reorientation:
	('reorient' name=ID)? exp=ReorientExpression
;

ReorientExpression:
	ref=[Reorientation] |
	'(' '(' axis+=AXIS ',' value += AddExp ')' (',' '(' axis+=AXIS ',' value += AddExp ')')* ')'
;

Constraint:
	'constraint' name=ID ':' type = ConstraintType
		'between' body1 = [Body] /* TODO Add support for 'base' */ relTrans1 = RelativeTransformation
		'and' body2 = [Body] relTrans2 = RelativeTransformation
;

enum ConstraintType:
	GEOMETRICAL = 'geometrical' |
	VELOCITY = 'velocity'
;

ExternalLoad:
	'external' 'load' name=ID ':' type = LoadType
		'between' body1 = [Body] /* TODO Add support for 'base' */ relTrans1 = RelativeTransformation
		'and' body2 = [Body] relTrans2 = RelativeTransformation	
;

enum LoadType:
	FORCE = 'force' |
	TORQUE = 'torque'
;

Matrix:
	BaseMatrix |
	MatrixRef
;

BaseMatrix:
	('matrix' name=ID)? '[' values+=AddExp (',' values+=AddExp)* ']'
;

MatrixRef:
	matrix=[BaseMatrix]
;

AddExp returns Expression:
	MultExp ({AddExp.left=current} op += ('+'|'-') right=MultExp)*
;

MultExp returns Expression:
	Primary ({MultExp.left=current} op += ('*'|'/') right=Primary)*
;

Primary returns Expression:
	NumberLiteral |
	'(' AddExp ')' |
	ConstantOrFunctionCallExp
;

ConstantOrFunctionCallExp:
	label=ID ('(' param += AddExp (',' param += AddExp)* ')')?
;

NumberLiteral:
	neg?='-'? value=REAL
;

// TODO Simplify format, possibly turn into a DataType rule...
terminal REAL /*returns ecore::EDouble*/: ('0'..'9')+ '.' ('0'..'9')+ (('e'|'E') ('+'|'-') ('0'..'9')* '.' ('0'..'9')+)?;